---
title: ECL commands for XML import and export
slug: xml-import-export
sidebar: userguide
layout: doc
menu:
  sidebar:
      parent: ecl
---
<br>
<h2>XML export</h2><hr>

### write-xml-file

Writes tables from input pipe to into xml file
<br><br>
Input:
<br>
<ul><li>**tree** Tree: Tree to write</li></ul>
Parameters:
<br>
<ul><li>**uri** String: URI to write xml data to. Currently supported schemes are workspace:/ for files in workspace and file:/ for files on local file system.</li></ul>
Output:
<ul><li>
The value of 'tree' argument</li></ul>
<br>
Example:

```ecl
proc newDevice [val id] [val name] [val target] {
    tree-node "Device"
        [map [entry "id" $id]]
        [tree-node -name "DeviceName" -text $name]
        [tree-node -name "DeviceTarget" -text $target]
}
 
 
global [val supportedDevices [tree-node "SupportedDevices"
    [newDevice "1" "device 1" "target 1"]
    [newDevice "2" "device 2" "target 2"]
    [newDevice "3" "device 3" "target 3"]
]]
 
global [val allDevices [tree-node "AllDevices"
    [newDevice "1" "device 1" "target 1"]
    [newDevice "2" "device 2" "target 2"]
    [newDevice "3" "device 3" "target 3"]
    [newDevice "4" "device 4" "target 4"]
    [newDevice "5" "device 5" "target 5"]
]]
 
global [val tree [
    tree-node "DevicesInfo" $supportedDevices $allDevices
]]
 
$tree | write-xml-file "workspace:/xml/devices.xml"

```

<br>
<h2>XML import</h2><hr>

### read-xml-file

Parses given xml file into tree and write it to output pipe. Fails if file is not found or if it is invalid xml.
<br><br>

Parameters:
<br>
<ul><li>**uri** String: URI to read xml data from. Currently supported schemes are workspace:/ for files in workspace and file:/ for files on local file system.</li></ul>
<ul><li>**xPath** String: XPath expression to find xml nodes</li></ul>

Output:
<ul><li>
Tree EMF Object</li></ul>
<br>
Example:

{% set snippet %}

proc logDevice [val device] {
    if [$device | has-attr "id"] {
        log [format "Device ID: %s" [$device | get-attr "id"]]
    }
    log [format "Device Name: %s" [$device | get-nodes "DeviceName" | get text]]
    log [format "Device Target: %s" [$device | get-nodes "DeviceTarget"  | get text]]
}
 
 
global [val supportedDevices [
    read-xml-file "workspace:/xml/devices.xml"
        | get-nodes "SupportedDevices" | get-nodes "Device" | to-list
]]
 
$supportedDevices | each [val device] {
    logDevice $device
}
{% endset %}

<br>
<h2>Additional commands</h2><hr>

### tree-node

Creates a new tree node
<br><br>
Input:
<br><ul>
<li>**name** String: Name of the new node</li>
<li>**attrs** optional EclMap: Map with node attributes</li>
<li>**children** (0, ∞) Tree: Children of the new node</li>
<li>**text** optional String: Text of the new node</li>
</ul>

Output:
<br><ul>
<li>Tree EMF Object</li></ul>
<br>
Example:

```ecl
tree-node "Device"
    [map [entry "id" "1"]]
    [tree-node -name "DeviceName" -text "device"]
    [tree-node -name "DeviceTarget" -text "target"]

```

### get-attrs

Gets attributes from the object
<br><br>
Input:
<br><ul>
<li>**object** EObject: Object to get attributes from</li>

</ul>

Output:
<br><ul>
<li>Map with object attributes</li></ul>
<br>
Example:

```ecl
tree-node "Device" [map [entry "id" "1"] [entry "name" "first"]]
    | get-attrs | get "name" | log

```

### get-attr

Gets attribute from the tree node by name. Fails if attribute is not found.
<br><br>
Input:
<br><ul>
<li>**object** EObject: Object to get attribute from</li>

</ul>

Parameters:
<br>
<ul><li>**name** String: Name of the attribute</li></ul>

Output:
<br><ul>
<li>Value of the attribute</li></ul>
<br>
Example:

```ecl
tree-node "Device" [map [entry "id" "1"] [entry "name" "first"]]
    | get-attr "name" | log

```

### set-attr

Sets value of the object attribute. If 'value' parameter is not specified, the attribute will be removed.
<br><br>
Input:
<br><ul>
<li>**object** EObject: Object to set attribute to</li>

</ul>

Parameters:
<br>
<ul><li>**name** String: Name of the attribute</li>
<li>**value** optional String: New value of the attribute</li>

</ul>

Output:
<br><ul>
<li>The value of 'object' argument</li></ul>
<br>
Example:

```ecl
tree-node "Device" [map [entry "id" "1"] [entry "name" "first"]]
    | set-attr "name" "second" | get-attr "name" | log

```

### has-attr

Checks if the object has attribute<br><br>
Input:
<br><ul>
<li>**object** EObject: Object to check attribute from</li>

</ul>

Parameters:
<br>
<ul><li>**name** String: Name of the attribute</li>

</ul>

Output:
<br><ul>
<li>true when attribute is exist, false otherwise</li></ul>
<br>
Example:

```ecl
tree-node "Device" [map [entry "id" "1"] [entry "name" "first"]]
    | has-attr "name" | log

```

### get-nodes

Gets child nodes from the object and writes them into output pipe. Fails if 'pos' of 'len' parameter is out of range.

<br><br>
Input:
<br><ul>
<li>**object** EObject: Object to get child nodes from</li>

</ul>

Parameters:
<br>
<ul>

<li>**name** optional String: Name of the nodes</li>
<li>**pos** optional Int: Start position. If 'name' attribute is provided, 'pos' is relative to the node list filtered by name.</li>
<li>**len** optional Int: Count of nodes. Default value is '-1' and it means the end of the node list. If 'name' attribute is provided, 'len' is relative to the node list filtered by name.</li>
</ul>

Output:
<br><ul>
<li>Child nodes of the object</li></ul>
<br>
Example:

```ecl
proc newDevice [val id] [val name] [val target] {
    tree-node "Device"
        [map [entry "id" $id]]
        [tree-node -name "DeviceName" -text $name]
        [tree-node -name "DeviceTarget" -text $target]
}
 
global [val supportedDevices [tree-node "SupportedDevices"
    [newDevice "1" "device 1" "target 1"]
    [newDevice "2" "device 2" "target 2"]
    [newDevice "3" "device 3" "target 3"]
]]
 
$supportedDevices | get-nodes "Device" -pos 1 -len 1
    | get-nodes "DeviceName" | get text
    | equals "device 2" | verify-true

```

### append

Adds child nodes to the object. Fails if 'index' parameter is out of range.

<br><br>
Input:
<br><ul>
<li>**object** EObject: Object to add child nodes to</li>

</ul>

Parameters:
<br>
<ul>

<li>**children** (1, ∞) Tree: New child nodes to add to the object</li>
<li>**index** optional Int: Index of the first child node added in the node list. Min value is '0' and max value is size of node list. Default value is '-1' and it means size of node list.</li>
</ul>

Output:
<br><ul>
<li>The value of 'object' argument</li></ul>
<br>
Example:

```ecl
tree-node "Device" [tree-node -name "DeviceName" -text "device"]
    | append [tree-node -name "DeviceTarget" -text "target"] -index 0


```

### remove

Removes child node from the object by index. Fails if 'index' parameter is out of range.

<br><br>
Input:
<br><ul>
<li>**object** EObject: Object to remove child node from</li>

</ul>

Parameters:
<br>
<ul>

<li>**index** Int: Index of the child node. Min value is '0' and max value is the last index in the node list. Default value is '-1' and the last index in the node list.</li>
</ul>

Output:
<br><ul>
<li>The value of 'object' argument</li></ul>
<br>
Example:

```ecl
tree-node "Device"
    [tree-node -name "DeviceName" -text "device"]
    [tree-node -name "DeviceTarget" -text "target"]
        | remove -index 1

```
{{m.ecl("#{snippet}")}}